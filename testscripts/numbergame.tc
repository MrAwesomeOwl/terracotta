FUNCTION numbergame;

line i: num = saved [%default number];

default:SendMessage("The number game!! compiled with terracotta");

repeat Forever {
    line i += 1;
    line rainbowIndex += 1;

    line timeUnit = "Seconds";
    default:SendActionBar(txt"The number: <green>" + line i);

    if (default ? IsSprinting) {
        line rngResult = num:Random(1, 20);

        #fish mode
        if (default ? IsSwimming) {
            #increase the number EVEN FASTER!
            line i += 12345678912345;

            default:SetRemainingAir(line rngResult * 30);

            line msg = txt:ParseExpression("<blue>" + "!"*line rngResult + "    <aqua>FISHMODE!!!!!! <rainbow:%var(rainbowIndex)>" + line i + "    <blue>" + "!"*line rngResult);
        }
        #normal sprint mode 
        else {
            line msg = txt:ParseExpression("<light_purple>" + "!"*line rngResult + "    <white>The number: <rainbow:%var(rainbowIndex)>" + line i + "    <light_purple>" + "!"*line rngResult);
        }

        default:SendActionBar(line msg: txt);

        line timeUnit = "Ticks"; #FAST
    }

    if (default ? IsSneaking) {
        default:PlaySound(snd["Anvil Land"]);
        default:SendActionBar(txt"<red>You have killed the loop!!! How dare you");
        break;
    }

    if (line i < 0) {
        line rngResult = num:Random(1, 200);
        line msg = txt:ParseExpression("<red><obfuscated>" + "!"*line rngResult + "    <reset><dark_red>what have you done... <#FF0000>" + line i + "    <red><obfuscated>" + "!"*line rngResult);
        default:SendActionBar(line msg: txt);
    }

    saved [%default number] = line i;

    #reset code
    if (default ? IsStandingOnBlock("nether_wart_block")) {
        #launching code
        default:LaunchTowardLocation(loc:Align(default.Location - vec[0,1,0]), -100);
        default:GivePotionEffect(pot["Slow Falling",1,2 * 20]);

        if (line i < 0) {
            default:SendActionBar("&c&lFIRST, FIX YOUR MISTAKES");
            line timeUnit = "Seconds";
        } else {
            line i = 0;
            default:SendMessage("You have been reset");
            default:SendActionBar; #clear action bar
        }
    }

    wait(1){Time Unit = line timeUnit ? Seconds};
}